
＊0つつかれ
＞メインメニュー

＊メインメニュー
：\_q\cやる気　（call,gauge,（やる気度）,100）
\n\![*]ゴースト変更　\![*]\q[No.1,ゴースト１に]　φ
（when、（ゴースト２存在）==1、\![*]\q[No.2,ゴースト２に]）　φ
（when、（ゴースト３存在）==1、\![*]\q[No.3,ゴースト３に]）
\n[half]\![*]\q[開発頻度変更,喋る頻度変更]
\n[half]\![*]\q[バグ報告,OnWebClap]
\n[half]\![*]\q[新しいゴーストを作る,新ゴ作成]
\n[half]\![*]\q[リセット,リセット]
\n\![*]\q[閉じる,なんでもないよ]
\_q

＊ゴースト１に
＄開発ゴースト	1
＄デフォルトサーフェス1	（ゴースト１さくら）
＄デフォルトサーフェス2	（ゴースト１うにゅう）
\p[1]（（ゴースト１さくら））
\p[2]（（ゴースト１うにゅう））


＊ゴースト２に
＄開発ゴースト	2
＄デフォルトサーフェス1	（ゴースト２さくら）
＄デフォルトサーフェス2	（ゴースト２うにゅう）
\p[1]（（ゴースト２さくら））
\p[2]（（ゴースト２うにゅう））

＊ゴースト３に
＄開発ゴースト	3
＄デフォルトサーフェス1	（ゴースト３さくら）
＄デフォルトサーフェス2	（ゴースト３うにゅう）
\p[1]（（ゴースト３さくら））
\p[2]（（ゴースト３うにゅう））

＊新ゴ作成
：\s[5]よぉーっし！\w9\w6
コツは掴んだし、\w3新しいゴースト作ってみようかな！\w9\w6
＞これ以上は無理	（ゴースト３存在）==1
＞初期シェル選択3	（ゴースト２存在）==1
＞初期シェル選択２

＊これ以上は無理
：
……\s[4]って流石にこれ以上増やすのは無理ね。\w9\w6
ちょっともう面倒見きれないわ。

＊喋る頻度変更
：\s[0]さて、どんな感じで行こうかしらね。
\n\![*]\q[怒涛の,６０秒おき]
\![*]\q[適度に,１８０秒おき]
\![*]\q[のんびり,３００秒おき]
\n[half]\![*]\q[休む,黙ってて]
\n\![*]\q[変えない,やっぱりやめる]

＊６０秒おき
＄喋り間隔	６０秒
：\s[7]よぉーし！\w5今日はやってやるわよー！！

＊１８０秒おき
＄喋り間隔	１８０秒
：\s[0]よし、適度に他のことしながらやりましょ。

＊３００秒おき
＄喋り間隔	３００秒
：\s[8]まあ、その内にいいアイデア出るでしょ。

＊黙ってて
＄喋り間隔	０秒
：\s[6]一休み、一休み。\w9\w6
慌てない、慌てない。

＊リセット
：\_q(全ゴーストを一から作り直しますがよろしいですか？)

\![*]\q[はい,リセット確定]　\![*]\q[いいえ,なんでもないよ]　
\_q

＊リセット確定
（ステータスリセット）
\p[1]\s[-1]
\p[2]\s[-1]
＞初期シェル選択

＊やっぱりやめる
＞メインメニュー

＊なんでもないよ
：\e

＊OnWebClap
\![open,inputbox,拍手送信内容,-1,いいね！,--limit=1000]

＊拍手送信内容を教えてもらった
\![execute,http-post,http://clap.webclap.com/clap.php?id=etwaslangsam,--param=message_body="にしき＠（拍手送信内容）",--async=webclap]
＄拍手送信内容	

＊拍手送信内容を空打ちされた
：\_q(キャンセルしました)\_q

＊拍手送信完了
：\s[4]……バグ報告が来てるわね。\w9\w6
対処しないとね。

＊拍手送信失敗
：\_q(「（R4）」によって失敗しました)\_q

＊OnExecuteHTTPComplete
＞拍手送信完了	（R1）==webclap

＊OnExecuteHTTPFailure
＞拍手送信失敗	（R1）==webclap

＠gauge
（call、do、
\f[color,（call,gauge_color,（A0）,（A1））]
（times、（A0）/（gauge_unit）、
\_w[（gauge_delay）]\_l[@（gauge_shift）]（gauge_fragment）
）
\f[color,default]
（when,（A0）==（A1）, [MAX],[（A0）/（A1）]）
）

＠gauge_unit
5

＠gauge_delay
1

＠gauge_shift
-5

＠gauge_fragment
■

＠gauge_color
#（iflist,（A0）*100/（A1）
,<=10,ff0000
,<=20,f39800
,<=30,e0a000
,<=40,d0d000
,<=50,50ff00
,<=70,00ffff
,<100,0000ff
,==100,ff00ff
）

＠do
（A0）

