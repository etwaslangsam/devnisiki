メニュー

＠めにゅー色
\f[cursorstyle,underline]\f[cursorpencolor,red]\f[cursorfontcolor,blue]

＊0つつかれ
＞メインメニュー

＊メインメニュー
：\_q\c【やる気】　（call,gauge,（やる気度）,100）（めにゅー色）
（when、（ゴースト２名前）!=0、　◯ゴースト変更　（when、（ゴースト１名前）!=0、\![*]\q[No.1,ゴースト１に]）　φ
（when、（ゴースト２名前）!=0、\![*]\q[No.2,ゴースト２に]）　φ
（when、（ゴースト３名前）!=0、\![*]\q[No.3,ゴースト３に]））
\n[half]\![*]\q[ステータス確認,ステータス確認]
\n[half]\![*]\q[開発計画,喋る頻度変更]
（when、（トーク数）>=50&&（ゴースト２名前）!=0、\![*]\q[新しいゴーストを作る,新ゴ作成]）φ
（when、（トーク数２）>=50&&（ゴースト３名前）!=0、\![*]\q[新しいゴーストを作る,新ゴ作成]）
（when、（ゴースト２名前）!=0、\![*]
（whenlist、（開発ゴースト）==
、1、\q[このゴーストを削除,ゴースト１削除]
、2、\q[このゴーストを削除,ゴースト２削除]
、3、\q[このゴーストを削除,ゴースト３削除]
）
）
\![*]\q[バグ報告,OnWebClap]
\n\![*]\q[閉じる,なんでもないよ]
\_q

＊ゴースト１に
＄開発ゴースト	1
＄デフォルトサーフェス1	（ゴースト１さくら）
＄デフォルトサーフェス2	（ゴースト１うにゅう）
\p[1]（（ゴースト１さくら））
\p[2]（（ゴースト１うにゅう））


＊ゴースト２に
＄開発ゴースト	2
＄デフォルトサーフェス1	（ゴースト２さくら）
＄デフォルトサーフェス2	（ゴースト２うにゅう）
\p[1]（（ゴースト２さくら））
\p[2]（（ゴースト２うにゅう））

＊ゴースト３に
＄開発ゴースト	3
＄デフォルトサーフェス1	（ゴースト３さくら）
＄デフォルトサーフェス2	（ゴースト３うにゅう）
\p[1]（（ゴースト３さくら））
\p[2]（（ゴースト３うにゅう））

＊新ゴ作成
：\s[5]よぉーっし！\w9\w6
コツは掴んだし、\w3新しいゴースト作ってみようかな！\w9\w6
＞初期シェル選択	（ゴースト１名前）==0
＞初期シェル選択2	（ゴースト２名前）==0
＞初期シェル選択3	（ゴースト３名前）==0
＞これ以上は無理


＊これ以上は無理
：
……\s[4]って流石にこれ以上増やすのは無理ね。\w9\w6
ちょっともう面倒見きれないわ。

＊喋る頻度変更
：\s[0]さて、どんな感じで行こうかしらね。\w9\_q
\c【開発頻度】（めにゅー色）
　\![*]\q[怒涛の,６０秒おき]　\![*]\q[適度に,１８０秒おき]　\![*]\q[のんびり,３００秒おき]　\![*]\q[休む,黙ってて]

【開発の方向性】
　\![*]\q[トーク中心,トーク中心]
　\![*]\q[イベント中心,イベント中心]
　\![*]\q[さわり反応中心,さわり反応中心]
　\![*]\q[コミュニケート中心,コミュニケート中心]

\q[閉じる,やっぱりやめる]
\_q

＊６０秒おき
＄喋り間隔	６０秒
：\s[7]よぉーし！\w5今日はやってやるわよー！！

＊１８０秒おき
＄喋り間隔	１８０秒
：\s[0]よし、適度に他のことしながらやりましょ。

＊３００秒おき
＄喋り間隔	３００秒
：\s[8]まあ、その内にいいアイデア出るでしょ。

＊黙ってて
＄喋り間隔	０秒
：\s[6]一休み、一休み。\w9\w6
慌てない、慌てない。

＊トーク中心
＄開発傾向	トーク
：\s[7]よぉーし！\w9
（選択ラベル）を中心に増やしていくわよ！

＊イベント中心
＄開発傾向	イベント
：\s[7]よぉーし！\w9
（選択ラベル）を中心に増やしていくわよ！

＊さわり反応中心
＄開発傾向	さわり
：\s[7]よぉーし！\w9
（選択ラベル）を中心に増やしていくわよ！

＊コミュニケート中心
＄開発傾向	コミュ
：\s[7]よぉーし！\w9
（選択ラベル）を中心に増やしていくわよ！

＊リセット
：\_q(全ゴーストを一から作り直しますがよろしいですか？)

\![*]\q[はい,リセット確定]　\![*]\q[いいえ,なんでもないよ]　
\_q

＊リセット確定
（ステータスリセット）
\p[1]\s[-1]
\p[2]\s[-1]
＞初期シェル選択

＊やっぱりやめる
＞メインメニュー

＊なんでもないよ
：\e

＊OnWebClap
\![open,inputbox,拍手送信内容,-1,いいね！,--limit=1000]

＊拍手送信内容を教えてもらった
\![execute,http-post,http://clap.webclap.com/clap.php?id=etwaslangsam,--param=message_body="にしき＠（拍手送信内容）",--async=webclap]
＄拍手送信内容	

＊拍手送信内容を空打ちされた
：\_q(キャンセルしました)\_q

＊拍手送信完了
：\s[4]……バグ報告が来てるわね。\w9\w6
対処しないとね。

＊拍手送信失敗
：\_q(「（R4）」によって失敗しました)\_q

＊OnExecuteHTTPComplete
＞拍手送信完了	（R1）==webclap

＊OnExecuteHTTPFailure
＞拍手送信失敗	（R1）==webclap

＠gauge
（call、do、
\f[color,（call,gauge_color,（A0）,（A1））]
（times、（A0）/（gauge_unit）、
\_w[（gauge_delay）]\_l[@（gauge_shift）]（gauge_fragment）
）
\f[color,default]
（when,（A0）==（A1）, [MAX],[（A0）/（A1）]）
）

＠gauge_unit
5

＠gauge_delay
1

＠gauge_shift
-5

＠gauge_fragment
■

＠gauge_color
#（iflist,（A0）*100/（A1）
,<=10,ff0000
,<=20,f39800
,<=30,e0a000
,<=40,d0d000
,<=50,50ff00
,<=70,00ffff
,<100,0000ff
,==100,ff00ff
）

＠do
（A0）

